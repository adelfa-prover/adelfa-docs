---
title: Lambda calculus terms have a size
description: Lambda calculus example
---

import { Tabs, Callout } from 'nextra/components';

# Every $\lambda$-calculus Term Has a Size

In this example we will discuss _schema subsumption_, a capability in Adelfa
which allows one to "transport" a formula with a context variable typed by one
context schema to a different context schema. This becomes especially useful
when using lemmas.

To show the usefulness of schema subsumption, we formalize a specification
for the size of lambda calculus terms and prove that every term has a size. The
informal rules for the size of terms is:

```math
|x| = 1 \qquad |\lambda x. e| = 1 + |e| \qquad |e_1\ e_2| = 1 + |e_1| + |e_2|
```

Where $e$ ranges over terms and $x$ denotes a variable.

Before we can specify the size judgements in LF, we have to specify natural
numbers and addition. We have seen these specified in [first order
examples](/examples/first-order/ident) so will limit the discussion here to a
presentation of the specification.

<a id="nat-sig"></a>
```lf filename="nat.lf"
nat : type.
z : nat.
s : {x:nat} nat.

plus : {x1:nat}{x2:nat}{x3:nat} type.
plus-z : {x: nat} plus z x x.
plus-s : {x1:nat}{x2:nat}{x3:nat}
         {D: plus x1 x2 x3} plus (s x1) x2 (s x3).
```

Natural numbers and addition are common enough relations that we would want to
have a standard library[^1] which defines them along with its properties. Foreseeing
our eventual proof, we introduce a single theorem which states "for any two
natural numbers, there exists a natural number which is their sum."

[^1]: Adelfa does not currently have the ability to compose specifications or
theorem developments which would permit libraries, so we inline these portions
in lieu of this feature.

<a id="plus-exist"></a>
```adelfa filename="size.ath" showOutput
Theorem plus-exist : forall N1 N2,
  {N1: nat} => {N2:nat} => exists N3 D, {D: plus N1 N2 N3}.
induction on 1.
intros.
case H1.
apply IH to H3 H2.
exists s N3.
exists plus-s x N2 N3 D.
search.
exists N2.
exists plus-z N2. search.

%{
Welcome!
>> Theorem plus-exist : forall N1 N2,
  {N1: nat} => {N2:nat} => exists N3 D, {D: plus N1 N2 N3}.

Subgoal plus-exist:


==================================
forall N1, forall N2,
  {N1 : nat} => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}

plus-exist>> induction on 1.

Subgoal plus-exist:

IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}

==================================
forall N1, forall N2,
  {N1 : nat}@ => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}

plus-exist>> intros.

Subgoal plus-exist:

Vars: N2:o, N1:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H1:{N1 : nat}@
H2:{N2 : nat}

==================================
exists N3, exists D, {D : plus N1 N2 N3}

plus-exist>> case H1.

Subgoal plus-exist.1:

Vars: x:o, N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}
H3:{x : nat}*

==================================
exists N3, exists D, {D : plus (s x) N2 N3}

Subgoal plus-exist.2 is:
 exists N3, exists D, {D : plus z N2 N3}

plus-exist.1>> apply IH to H3 H2.

Subgoal plus-exist.1:

Vars: D:o, N3:o, x:o, N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}
H3:{x : nat}*
H4:{D : plus x N2 N3}

==================================
exists N3, exists D, {D : plus (s x) N2 N3}

Subgoal plus-exist.2 is:
 exists N3, exists D, {D : plus z N2 N3}

plus-exist.1>> exists s N3.

Subgoal plus-exist.1:

Vars: D:o, N3:o, x:o, N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}
H3:{x : nat}*
H4:{D : plus x N2 N3}

==================================
exists D, {D : plus (s x) N2 (s N3)}

Subgoal plus-exist.2 is:
 exists N3, exists D, {D : plus z N2 N3}

plus-exist.1>> exists plus-s x N2 N3 D.

Subgoal plus-exist.1:

Vars: D:o, N3:o, x:o, N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}
H3:{x : nat}*
H4:{D : plus x N2 N3}

==================================
{plus-s x N2 N3 D : plus (s x) N2 (s N3)}

Subgoal plus-exist.2 is:
 exists N3, exists D, {D : plus z N2 N3}

plus-exist.1>> search.

Subgoal plus-exist.2:

Vars: N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}

==================================
exists N3, exists D, {D : plus z N2 N3}

plus-exist.2>> exists N2.

Subgoal plus-exist.2:

Vars: N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}

==================================
exists D, {D : plus z N2 N2}

plus-exist.2>> exists plus-z N2.

Subgoal plus-exist.2:

Vars: N2:o
IH:
    forall N1, forall N2,
      {N1 : nat}* => {N2 : nat} => exists N3, exists D, {D : plus N1 N2 N3}
H2:{N2 : nat}

==================================
{plus-z N2 : plus z N2 N2}

plus-exist.2>> search.
Proof Completed!
}%
```

With this standard library now in place, we can specify our $\lambda$-calculus
terms and their size. Their specification is a straightforward translation from
our informal rules.

<a id="size-sig"></a>
```lf filename="size.lf"
tm : type.

app : {E1:tm}{E2:tm} tm.
lam : {E: {x:tm} tm} tm.

size : {E:tm}{x:nat} type.
size-lam : {E: {y:tm} tm}{X:nat}
           {D:{x:tm} {dx: size x (s z)} size (E x) X}
           size (lam ([x] E x)) (s X).
size-app : {E1:tm}{E2:tm}{X1:nat}{X2:nat}{X3:nat}
           {D1: size E1 X1}
           {D2: size E2 X2}
           {D3: plus X1 X2 X3}
           size (app E1 E2) (s X3).
```

Before we walk through our goal of showing every term has a size in Adelfa,
let's walk through the informal proof.

> _Theorem_: Every $\lambda$-calculus term $e$ has a size $n$.
> 
> _Proof_: By induction on the size of terms. Case analysis on $e$:
> - If $e$ is a variable, $|x| = 1$ by definition
> - If $e$ is $|\lambda x. e'|$, by inductive hypothesis, there is some $n'$ such
>   that $n' = |e'|$. By definition, $|\lambda x. e'| = 1 + n'$
> - If $e$ is $|e_1\ e_2|$, then by inductive hypothesis on the component terms,
>   there is some $n_1$ and $n_2$ such that $|e_1| = n_1$ and $|e_2| = n_2$. By
>   definition, $|e_1\ e_2| = 1 + n_1 + n_2$
>
> Q.E.D.

When proving this in Adelfa, we must be more precise about our statement. We
permit open terms in our informal proof and categorize all variables as having a
size of $1$. This doesn't suffice in a formal setting, so we limit ourselves
to closed terms and prove our statement for terms under a context which forces
the size of terms to be assessed under a context with variables of size 1. This is
encoded within the `size-lam` term constructor. Reasoning inductively over
derivations of `size` then results in a regular shape for contexts:

```math
(x_1: \mathtt{tm}, y_1: \mathtt{size}\ x_1\ (\mathtt{s}\ \mathtt{z}), \ldots, x_n: \mathtt{tm}, y_n: \mathtt{size}\ x_n\ (\mathtt{s}\ \mathtt{z}))
```

We specify such a shape in adelfa by defining a context schema `C`, with which we
we lay out the entries of a single repetition.

```adelfa filename="size.ath"
Schema C := (x: tm, y: size x (s z)).
```


Our proof in Adelfa aligns closely with our informal proof. We begin by
performing induction on the derivation of the of `E`, introducing the
eigenvariables, context variable, and implication as hypotheses, then performing
case analysis on the derivation of `E`. Our first proof obligation is that of an
abstraction. To keep this example concise, we will skip the proof of the
abstraction and move into the more interesting case of an application.


```adelfa filename="size.ath" showOutput
Theorem tm-has-size : ctx G:C, forall E,
  {G |- E : tm} => exists N D, {G |- D: size E N}.
  induction on 1. intros.
  case H1 (keep). skip.

%{

>> Theorem tm-has-size : ctx G:C, forall E,
  {G |- E : tm} => exists N D, {G |- D: size E N}.

Subgoal tm-has-size:


==================================
ctx G:C, forall E, {G |- E : tm} => exists N, exists D, {G |- D : size E N}

tm-has-size>> induction on 1.

Subgoal tm-has-size:

IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}

==================================
ctx G:C, forall E, {G |- E : tm}@ => exists N, exists D, {G |- D : size E N}

tm-has-size>> intros.

Subgoal tm-has-size:

Vars: E:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- E : tm}@

==================================
exists N, exists D, {G |- D : size E N}

tm-has-size>> case H1 (keep).

Subgoal tm-has-size.1:

Vars: E1:(o) -> o
Nominals: n:o
Contexts: G{n}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> skip.

Subgoal tm-has-size.2:

Vars: E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}
}%
```

To complete the application case, we apply the  inductive hypothesis to the
assumptions `{G |- E1 : tm}` and `{G |- E2 : tm}`.

```adelfa filename="size.ath" showOutput
apply IH to H2. apply IH to H3.

%{

tm-has-size.2>> apply IH to H2.

Subgoal tm-has-size.2:

Vars: D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply IH to H3.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}
}%
```

We want to instantiate the existentially quantified `N` with the successor of
the sum for `N` and `N1`, but we are missing a derivation for the sum. In our
library, we have a proof of this `plus-exist`. Let's get derivations for these
natural numbers and attempt to utilize this lemma.


```adelfa filename="size.ath" showOutput
assert { G |- N : nat }.
assert { G |- N1 : nat }.
apply plus-exist to H6 H7.

%{

tm-has-size.2>> assert { G |- N : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> assert { G |- N1 : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply plus-exist to H6 H7.

Error: could not match context variables
}%
```

The resulting error "Error: could not match context variables" arises because
`plus-exist` was proven for LF derivations without any context variable. As
such, Adelfa cannot verify the proof still holds in the presence of this context
variable. In the next section we'll walk through, step by step, how one may
attempt to fix this situation and discuss them.

## Proof Without Transport

In this section, we will discuss two ways to continue this proof without
utilizing the transport rule and see where approaches differ and where they fall
short.

### Removing context dependencies

The first solution one may try is to prove that the natural numbers do not rely
on entries in the schema `C` to use this lemma. Such a proof is fairly routine
by induction and case analysis on the derivation for natural numbers.

```adelfa filename="size.ath" showOutput
Theorem nat-ind : ctx G:C, forall N,
  { G |- N : nat } => { N : nat }.
induction on 1. intros.
case H1. apply IH to H2. search. search.

%{

>> Theorem nat-ind : ctx G:C, forall N,
  { G |- N : nat } => { N : nat }.

Subgoal nat-ind:


==================================
ctx G:C, forall N, {G |- N : nat} => {N : nat}

nat-ind>> induction on 1.

Subgoal nat-ind:

IH:ctx G:C, forall N, {G |- N : nat}* => {N : nat}

==================================
ctx G:C, forall N, {G |- N : nat}@ => {N : nat}

nat-ind>> intros.

Subgoal nat-ind:

Vars: N:o
Contexts: G{}:C[]
IH:ctx G:C, forall N, {G |- N : nat}* => {N : nat}
H1:{G |- N : nat}@

==================================
{N : nat}

nat-ind>> case H1.

Subgoal nat-ind.1:

Vars: x:o
Contexts: G{}:C[]
IH:ctx G:C, forall N, {G |- N : nat}* => {N : nat}
H2:{G |- x : nat}*

==================================
{s x : nat}

Subgoal nat-ind.2 is:
 {z : nat}

nat-ind.1>> apply IH to H2.

Subgoal nat-ind.1:

Vars: x:o
Contexts: G{}:C[]
IH:ctx G:C, forall N, {G |- N : nat}* => {N : nat}
H2:{G |- x : nat}*
H3:{x : nat}

==================================
{s x : nat}

Subgoal nat-ind.2 is:
 {z : nat}

nat-ind.1>> search.

Subgoal nat-ind.2:

Contexts: G{}:C[]
IH:ctx G:C, forall N, {G |- N : nat}* => {N : nat}

==================================
{z : nat}

nat-ind.2>> search.
Proof Completed!
}%
```

Let's back up to when we tried to use `plus-exist` and try to complete the proof
after removing the context variables. This time, the application of `plus-exist`
is allowed, and now have an assumption for `{D2 : plus N N1 N3}`.

```adelfa filename="size.ath" showOutput
assert { G |- N : nat }. apply nat-ind to H6.
assert { G |- N1 : nat }. apply nat-ind to H8.
apply plus-exist to H7 H9.

%{

tm-has-size.2>> assert { G |- N : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply nat-ind to H6.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{N : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> assert { G |- N1 : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{N : nat}
H8:{G |- N1 : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply nat-ind to H8.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{N : nat}
H8:{G |- N1 : nat}
H9:{N1 : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply plus-exist to H7 H9.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{N : nat}
H8:{G |- N1 : nat}
H9:{N1 : nat}
H10:{D2 : plus N N1 N3}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}
}%
```

Given `N3` is the sum of the sizes for `E1` and `E2`, the goal's existentially
quantified variables can now be instantiated with `N` as `(s N3)` and `D` as a
term constructor for `size-app`. This time, we cannot complete the case since
adelfa cannot find a derivation for:

```math
\mathtt{G} \vdash_{\Sigma} \mathtt{N3} \Leftarrow \mathtt{nat}
```

```adelfa filename="size.ath" showOutput
exists (s N3).
exists size-app E1 E2 N N1 N3 D D1 D2.
search.

%{

tm-has-size.2>> exists (s N3).

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{N : nat}
H8:{G |- N1 : nat}
H9:{N1 : nat}
H10:{D2 : plus N N1 N3}

==================================
exists D, {G |- D : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> exists size-app E1 E2 N N1 N3 D D1 D2.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{N : nat}
H8:{G |- N1 : nat}
H9:{N1 : nat}
H10:{D2 : plus N N1 N3}

==================================
{G |- size-app E1 E2 N N1 N3 D D1 D2 : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> search.

Error: Search failed. Unable to determine validity of: G |- N3 : nat
}%
```

In order to utilize `size-app` we must have `N3` under the same context variable
as the other terms. Yet, our only occurrence of `N3` in our assumption set is
within the formula `{D2 : plus N N1 N3}`. The best Adelfa can deduce is
therefore `{N3 : nat}`. One may try to prove a lemma of the sort:

```adelfa
Theorem nat-wk : ctx G:C, forall N, { N : nat } => { G |- N : nat}.
```

But this will not work. The instantiations for `G` are not limited to
well-formed ones, and therefore this statement is false. Therefore, the state we
are in within our proof of `tm-has-size` has reached a dead end, and we must
reevaluate how to proceed.


### Reproving lemmas

The second approach one may attempt is to reprove necessary lemmas under a
context variable.

<a id="plus-exist-ctx"></a>
```adelfa filename="size.ath" showOutput
Theorem plus-exist-ctx : ctx G:C, forall N1 N2,
  {G |- N1: nat} => {G |- N2:nat} => exists N3 D, {G |- D: plus N1 N2 N3}.
induction on 1.
intros.
case H1.
apply IH to H3 H2.
exists s N3.
exists plus-s x N2 N3 D.
search.
exists N2.
exists plus-z N2. search.

%{

>> Theorem plus-exist-ctx : ctx G:C, forall N1 N2,
  {G |- N1: nat} => {G |- N2:nat} => exists N3 D, {G |- D: plus N1 N2 N3}.

Subgoal plus-exist-ctx:


==================================
ctx G:C,
  forall N1, forall N2,
    {G |- N1 : nat} =>
        {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}

plus-exist-ctx>> induction on 1.

Subgoal plus-exist-ctx:

IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}

==================================
ctx G:C,
  forall N1, forall N2,
    {G |- N1 : nat}@ =>
        {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}

plus-exist-ctx>> intros.

Subgoal plus-exist-ctx:

Vars: N2:o, N1:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H1:{G |- N1 : nat}@
H2:{G |- N2 : nat}

==================================
exists N3, exists D, {G |- D : plus N1 N2 N3}

plus-exist-ctx>> case H1.

Subgoal plus-exist-ctx.1:

Vars: x:o, N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}
H3:{G |- x : nat}*

==================================
exists N3, exists D, {G |- D : plus (s x) N2 N3}

Subgoal plus-exist-ctx.2 is:
 exists N3, exists D, {G |- D : plus z N2 N3}

plus-exist-ctx.1>> apply IH to H3 H2.

Subgoal plus-exist-ctx.1:

Vars: D:o, N3:o, x:o, N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}
H3:{G |- x : nat}*
H4:{G |- D : plus x N2 N3}

==================================
exists N3, exists D, {G |- D : plus (s x) N2 N3}

Subgoal plus-exist-ctx.2 is:
 exists N3, exists D, {G |- D : plus z N2 N3}

plus-exist-ctx.1>> exists s N3.

Subgoal plus-exist-ctx.1:

Vars: D:o, N3:o, x:o, N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}
H3:{G |- x : nat}*
H4:{G |- D : plus x N2 N3}

==================================
exists D, {G |- D : plus (s x) N2 (s N3)}

Subgoal plus-exist-ctx.2 is:
 exists N3, exists D, {G |- D : plus z N2 N3}

plus-exist-ctx.1>> exists plus-s x N2 N3 D.

Subgoal plus-exist-ctx.1:

Vars: D:o, N3:o, x:o, N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}
H3:{G |- x : nat}*
H4:{G |- D : plus x N2 N3}

==================================
{G |- plus-s x N2 N3 D : plus (s x) N2 (s N3)}

Subgoal plus-exist-ctx.2 is:
 exists N3, exists D, {G |- D : plus z N2 N3}

plus-exist-ctx.1>> search.

Subgoal plus-exist-ctx.2:

Vars: N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}

==================================
exists N3, exists D, {G |- D : plus z N2 N3}

plus-exist-ctx.2>> exists N2.

Subgoal plus-exist-ctx.2:

Vars: N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}

==================================
exists D, {G |- D : plus z N2 N2}

plus-exist-ctx.2>> exists plus-z N2.

Subgoal plus-exist-ctx.2:

Vars: N2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall N1, forall N2,
        {G |- N1 : nat}* =>
            {G |- N2 : nat} => exists N3, exists D, {G |- D : plus N1 N2 N3}
H2:{G |- N2 : nat}

==================================
{G |- plus-z N2 : plus z N2 N2}

plus-exist-ctx.2>> search.
Proof Completed!
}%
```

Notice that there is _no difference_ in the proof compared to the [one without
the context variable](#plus-exist). This makes sense, because `nat`
and `plus` do not depend on anything within `G`.

Using this reproved lemma within our proof of `tm-has-size` will succeed this
time.

```adelfa filename="size.ath" showOutput
assert { G |- N : nat }.
assert { G |- N1 : nat }.
apply plus-exist-ctx to H6 H7.

%{

tm-has-size.2>> assert { G |- N : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> assert { G |- N1 : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply plus-exist-ctx to H6 H7.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}
}%
```

Having an assumption formula `{G |- D2 : plus N N1 N3}` allows us to properly 
instantiate the existentially quantified variables with `N` as `s N3` and `D` as
the term constructor `size-app`. This case can then be completed and we can move
on to the next proof obligation. Our previous issue where we could not show that
`N3` is a wellformed term of type `nat` in the contxt `G`. This is no longer an
issue now that we have the assumption `{G |- D2 : plus N N1 N3}`, which implies
`{G |- N3 : nat}`.


```adelfa filename="size.ath" showOutput
exists (s N3).
exists size-app E1 E2 N N1 N3 D D1 D2.

%{

tm-has-size.2>> exists (s N3).

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
exists D, {G |- D : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> exists size-app E1 E2 N N1 N3 D D1 D2.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:C[]
IH:
    ctx G:C,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
{G |- size-app E1 E2 N N1 N3 D D1 D2 : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}
}%
```

## Proof With Transport

With schema subsumption, when Adelfa encounters a usage of the `apply` tactic
which would fail due to a mismatch in context schemas, it will automatically try
to transport the proof into the new context schema. In our case, transporting
the proof of [`plus-exist`](#plus-exist) into one of
[`plus-exist-ctx`](#plus-exist-ctx) automatically.

The proof for `tm-has-size` then requires no extra lemmas and can directly
utilize the library's theorem without worrying about reproving routine
properties under a new context schema.

```adelfa filename="size.ath" showOutput
assert { G |- N : nat }.
assert { G |- N1 : nat }.
apply plus-exist to H6 H7.
exists (s N3).
exists size-app E1 E2 N N1 N3 D D1 D2.
search.

%{

tm-has-size.2>> assert { G |- N : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> assert { G |- N1 : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply plus-exist to H6 H7.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> exists (s N3).

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
exists D, {G |- D : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> exists size-app E1 E2 N N1 N3 D D1 D2.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
{G |- size-app E1 E2 N N1 N3 D D1 D2 : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> search.

Subgoal tm-has-size.3:

Nominals: n1:o, n:o
Contexts: G{}:c[(n:tm, n1:size n (s z))]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- n : tm}@

==================================
exists N, exists D, {G |- D : size n N}
}%
```

With transport, the entire proof is:


```adelfa filename="size.ath" showOutput
Theorem tm-has-size : ctx G:c, forall E,
  {G |- E : tm} => exists N D, {G |- D: size E N}.
induction on 1. intros.
case H1 (keep).
weaken H2 with size n (s z).
apply IH to H3 with (G = G, n1:tm, n:size n1 (s z)).
assert { G, n1:tm, n:size n1 (s z) |- N n1 n : nat }.
prune H5.
strengthen H5. strengthen H6.
exists (s N). exists size-lam ([x] E1 x) N D.
search.
apply IH to H2.
apply IH to H3.
assert { G |- N : nat }.
assert { G |- N1 : nat }.
apply plus-exist to H6 H7.
exists (s N3). exists size-app E1 E2 N N1 N3 D D1 D2. search.
exists (s z). exists n1. search.

%{

>> Theorem tm-has-size : ctx G:c, forall E,
  {G |- E : tm} => exists N D, {G |- D: size E N}.

Subgoal tm-has-size:


==================================
ctx G:c, forall E, {G |- E : tm} => exists N, exists D, {G |- D : size E N}

tm-has-size>> induction on 1.

Subgoal tm-has-size:

IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}

==================================
ctx G:c, forall E, {G |- E : tm}@ => exists N, exists D, {G |- D : size E N}

tm-has-size>> intros.

Subgoal tm-has-size:

Vars: E:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- E : tm}@

==================================
exists N, exists D, {G |- D : size E N}

tm-has-size>> case H1 (keep).

Subgoal tm-has-size.1:

Vars: E1:(o) -> o
Nominals: n:o
Contexts: G{n}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> weaken H2 with size n (s z).

Subgoal tm-has-size.1:

Vars: E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> apply IH to H3 with (G = G, n1:tm, n:size n1 (s z)).

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:(o) -> (o) -> o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) (N n1 n)}

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> assert { G, n1:tm, n:size n1 (s z) |- N n1 n : nat }.

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:(o) -> (o) -> o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) (N n1 n)}
H5:{G, n1:tm, n:size n1 (s z) |- N n1 n : nat}

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> prune H5.

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) N}
H5:{G, n1:tm, n:size n1 (s z) |- N : nat}

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> strengthen H5.

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) N}
H5:{G, n1:tm, n:size n1 (s z) |- N : nat}
H6:{G, n1:tm |- N : nat}

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> strengthen H6.

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) N}
H5:{G, n1:tm, n:size n1 (s z) |- N : nat}
H6:{G, n1:tm |- N : nat}
H7:{G |- N : nat}

==================================
exists N, exists D, {G |- D : size (lam ([c7]E1 c7)) N}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> exists (s N).

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) N}
H5:{G, n1:tm, n:size n1 (s z) |- N : nat}
H6:{G, n1:tm |- N : nat}
H7:{G |- N : nat}

==================================
exists D, {G |- D : size (lam ([c7]E1 c7)) (s N)}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> exists size-lam ([x] E1 x) N D.

Subgoal tm-has-size.1:

Vars: D:(o) -> (o) -> o, N:o, E1:(o) -> o
Nominals: n1:o, n:o
Contexts: G{n, n1}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- lam ([c2]E1 c2) : tm}@
H2:{G, n:tm |- E1 n : tm}*
H3:{G, n:tm, n1:size n (s z) |- E1 n : tm}*
H4:{G, n1:tm, n:size n1 (s z) |- D n1 n : size (E1 n1) N}
H5:{G, n1:tm, n:size n1 (s z) |- N : nat}
H6:{G, n1:tm |- N : nat}
H7:{G |- N : nat}

==================================
{G |- size-lam ([x]E1 x) N D : size (lam ([c7]E1 c7)) (s N)}

Subgoal tm-has-size.2 is:
 exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.1>> search.

Subgoal tm-has-size.2:

Vars: E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply IH to H2.

Subgoal tm-has-size.2:

Vars: D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply IH to H3.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> assert { G |- N : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> assert { G |- N1 : nat }.

Subgoal tm-has-size.2:

Vars: D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> apply plus-exist to H6 H7.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
exists N, exists D, {G |- D : size (app E1 E2) N}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> exists (s N3).

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
exists D, {G |- D : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> exists size-app E1 E2 N N1 N3 D D1 D2.

Subgoal tm-has-size.2:

Vars: D2:o, N3:o, D1:o, N1:o, D:o, N:o, E1:o, E2:o
Contexts: G{}:c[]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- app E1 E2 : tm}@
H2:{G |- E1 : tm}*
H3:{G |- E2 : tm}*
H4:{G |- D : size E1 N}
H5:{G |- D1 : size E2 N1}
H6:{G |- N : nat}
H7:{G |- N1 : nat}
H8:{G |- D2 : plus N N1 N3}

==================================
{G |- size-app E1 E2 N N1 N3 D D1 D2 : size (app E1 E2) (s N3)}

Subgoal tm-has-size.3 is:
 exists N, exists D, {G |- D : size n N}

tm-has-size.2>> search.

Subgoal tm-has-size.3:

Nominals: n1:o, n:o
Contexts: G{}:c[(n:tm, n1:size n (s z))]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- n : tm}@

==================================
exists N, exists D, {G |- D : size n N}

tm-has-size.3>> exists (s z).

Subgoal tm-has-size.3:

Nominals: n1:o, n:o
Contexts: G{}:c[(n:tm, n1:size n (s z))]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- n : tm}@

==================================
exists D, {G |- D : size n (s z)}

tm-has-size.3>> exists n1.

Subgoal tm-has-size.3:

Nominals: n1:o, n:o
Contexts: G{}:c[(n:tm, n1:size n (s z))]
IH:
    ctx G:c,
      forall E, {G |- E : tm}* => exists N, exists D, {G |- D : size E N}
H1:{G |- n : tm}@

==================================
{G |- n1 : size n (s z)}

tm-has-size.3>> search.
Proof Completed!
}%
```